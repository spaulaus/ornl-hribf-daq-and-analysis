C$PROG HISLOG
      SUBROUTINE HISLOG(NDAD,LHISH,MSER,IERR)
C
      COMMON/HHH/ KHP(4,20),LHP(4),KRA(2,4),KGS(2,4),KHT,NHP,NHS,
     &            ICTY(100),NORS(100),LOCC(100),LOCG(100),ITPR(2,100),
     &            LIMS(2,100),IFBID(100),NCON
      CHARACTER*4 ICTY
C
      COMMON/JJJ/ LIN,LOU,LU6,LER,NEREC,NSOL,LHLN,NERR
C
      COMMON/KKK/ MILF(262144),MILMF(32768),MILC,MILCF,NHWPC,NXDAD,Q,
     &            NUBPC,IGATOF,IMAPOF,NGATL,NMAPL,MXGATL,MXMAPL
C
      COMMON/QQQ/ ITEX(20),ITIT(10),NUID
C
      COMMON/YYY/ NAMHIL(20),LISKIN,LTA,LICO,NOHIS,ITRACE
C
      INTEGER*4 INDX(100),LHPP(2),KRAA(2,2),LIMI(2)
C
      INTEGER*4 KXH(3),KYH(3),KXT(3),KYT(3)
C
      INTEGER*4 MSG(10),MSER(10)
      CHARACTER*40 MSC(1)
C
      INTEGER*2 MILH(524288)
C
      EQUIVALENCE (MILH(1),MILF(1))
C
      EQUIVALENCE (LHPP(1),LHP(1)),
     &            (KRAA(1,1),KRA(1,1))
C
      EQUIVALENCE (MSC(1),MSG(1))
C
      DATA MSC/'ILLEGAL # OF HISTOGRAMS IN OVERLAY      '/
C
      DATA NLN,LNUID,MAXHID/100,0,0/
C
      CHARACTER*4  KHT
C
      SAVE
C
C     **************************************************************
C     PRODUCE HISTOGRAM SUMMARY AND GENERATE DIRECTORY (ULTIMATELY)
C     **************************************************************
C     KHT      = 'H   ' OR 'OH  ' - HISTOGRAM TYPE
C     NHP      = # OF HISTOGRAM PARAMETERS
C     NHS      = # OF HISTOGRAM SETS (TO OCCUPY SAME SPACE)
C     LNUID    = NUID ON ENTRY  FOR LAST CALL WITH KHT.NE.'OH  '
C     MAXHID   = NUID ON RETURN FOR LAST CALL WITH KHT.NE.'OH  '
C
C     KHP(J,K) = JTH HISTOGRAM PARAMETER OF THE KTH SET
C     LHP(J)   = LENGTH (PWR OF 2)   OF JTH HISTOGRAM PARM (SHIFTED)
C     KRA(1,J) = RANGE LO-LIMIT      OF JTH HISTOGRAM PARM (SHIFTED)
C     KRA(2,J) = RANGE HI-LIMIT      OF JTH HISTOGRAM PARM (SHIFTED)
C     KGS(1,J) = SELF-GATE LO-LIMIT FOR JTH HISTOGRAM PARM (RAW)
C     KGS(2,J) = SELF-GATE HI-LIMIT FOR JTH HISTOGRAM PARM (RAW)
C     **************************************************************
C     NCON      = # OF CONDITIONS
C
C     ICTY(J)   = CONDITION TYPE               FOR JTH CONDITION
C               = 'SING' SAYS SINGLE GATE
C               = 'LIST' SAYS GATE LIST
C               = 'BAN ' SAYS BAN  LIST
C               = 'MAP ' SAYS MAPPED GATE-LIST
C
C     NORS(J)   = # OF ENTRIES (GATES OR BANS) FOR JTH CONDITION
C     ITPR(1,J) = 1ST TEST PARAMETER           FOR JTH CONDITION
C     ITPR(2,J) = 2ND TEST PARAMETER (IF BAN)  FOR JTH CONDITION
C     LIMS(1,J) = LO-LIMIT (IF SINGLE GATE)    FOR JTH CONDITION
C     LIMS(2,J) = HI-LIMIT (IF SINGLE GATE)    FOR JTH CONDITION
C     IFBID(J)  = FIRST BAN-ID FOR JTH CONDITION
C     LOCC(J)   = FULL-WD MIL-LOC OF GATE-LIST, GATE-MAP, OR
C                 BAN-LIST FOR JTH CONDITION
C     LOCG(J)   = HALF-WD MIL-LOC OF GATE-LIST FOR JTH COND
C     **************************************************************
C
      IERR=0
      CALL PARCO(KHP(1,1),KXH)
      CALL PARCO(KHP(2,1),KYH)
C
      IF(KHT.NE.'OH  ') LNUID=NUID
      IF(KHT.EQ.'OH  ') NUID =LNUID
C
      IDO=1
      IF(NCON.GT.IDO) IDO=NCON
      IF(NCON.LE.0)   NORS(1)=0
C
      DO 20 I=1,IDO
      INDX(I)=1
   20 CONTINUE
      KDAD=NDAD
C
   50 J=1
C
C     **************************************************************
C
  100 NDO=1
      NUID=NUID+1
      IF(NCON.GT.1) NDO=NCON
C
      DO 200 N=1,NDO
      NLN=NLN+1
      IF(NLN.GE.60) THEN
                    WRITE(LTA,110)
                    NLN=0
                    ENDIF
C
  110 FORMAT(1H1,'  HID HT X-HIS-PAR   Y-HIS-PAR  WPC XLEN  YLEN  ...X-R
     &ANGE  ...Y-RANGE X-TST-PAR   Y-TST-PAR   FORM    LO    HI  LENG LI
     &NE#  DSKOFF')
      IF(NCON.GE.1) GO TO 120
C
      WRITE(LTA,115)NUID,KHT,KXH,KYH,NHWPC,LHPP,KRAA,LHLN,KDAD
  115 FORMAT(1H ,I5,1X,A2,2(1X,2A4,A3),I2,6I6,47X,I6,I8)
      GO TO 170
C
  120 CALL PARCO(ITPR(1,N),KXT)
      CALL PARCO(ITPR(2,N),KYT)
      LIMI(1)=LIMS(1,N)
      LIMI(2)=LIMS(2,N)
C
      IF(ICTY(N).EQ.'SING') GO TO 125
      IF(ICTY(N).EQ.'BAN ') GO TO 122
C
      MDX=2*INDX(N)+LOCG(N)-2
      LIMI(2)=MILH(MDX)
      LIMI(1)=MILH(MDX+1)
      GO TO 125
C
  122 LIMI(1)=IFBID(N)+INDX(N)-1
      LIMI(2)=LIMI(1)
C
  125 IF(N.NE.1) GO TO 150
C
      WRITE(LTA,140)NUID,KHT,KXH,KYH,NHWPC,LHPP,KRAA,KXT,KYT,
     &ICTY(N),LIMI,LHLN,KDAD
  140 FORMAT(1H ,I5,1X,A2,2(1X,2A4,A3),I2,6I6,2(1X,2A4,A3),1X,A4,
     &2I6,6X,I6,I8)
      GO TO 170
C
  150 WRITE(LTA,160)KXT,KYT,ICTY(N),LIMI
  160 FORMAT(1H ,70X,2(1X,2A4,A3),1X,A4,2I6)
      GO TO 200
C
  170 IF(KHT.NE.'OH  ') CALL DIRCO(KDAD,KXH,KYH)
C
  200 CONTINUE
C
C     **************************************************************
C
      KDAD=KDAD+LHISH
C
      INDX(J)=INDX(J)+1
      IF(INDX(J).LE.NORS(J)) GO TO 100
C
      INDX(J)=1
  400 J=J+1
      IF(J.GT.NCON) GO TO 450
      INDX(J)=INDX(J)+1
      IF(INDX(J).LE.NORS(J)) GO TO 50
      INDX(J)=1
      GO TO 400
C
  450 IF(KHT.EQ.'OH  ') GO TO 460
      MAXHID=NUID
      RETURN
C
  460 IF(NUID.EQ.MAXHID) RETURN
      IERR=1
      DO 470 I=1,10
      MSER(I)=MSG(I)
  470 CONTINUE
      RETURN
      END
