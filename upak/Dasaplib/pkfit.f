C$PROG PKFIT
      SUBROUTINE PKFIT
C
      COMMON/MAINV8/ A(50,50),B(50,50),DET,IFS
      REAL*8         A,       B,       DET
C
      COMMON/AAX/ SPECT(256,19),ISPK(16384),IBAK(16384),ICAL(16384)
      COMMON/BBB/ XAG(250,7),NPK
      COMMON/CCC/ BGD(256),WT(256),YCAL(256),GAU(200),NG(16)
      COMMON/DDD/ IHOL(16),XP(16),AREA(16),BETA(18),LOCA(250),BIAS
      COMMON/EEE/ ID,ILO,IHI,KWD,FWHMA,FWHMB,EOOO,GAINN,WDNC,DELCH,BSTD
      COMMON/FFF/ HWID(16),QFN,QFLO,IFBGD,NTRY,MSN,NCH,KPK,I1,I2
      COMMON/GGG/ MXNPK,MXRGL,MXPIS,NBNF
      COMMON/HHH/ IGAUS,KNBAK,KNVAR,KVW
C
      DIMENSION WDSQ(16)
C
      DATA JN/0/
C
      SAVE
C
C     THIS SUBROUTINE FITS THE SPECTRUM RANGE I1 TO I2 TO A FORM
C     C1+ C2*X+C3*G1(I)+C4*G2(I)+ ---, WHERE G1,G2,--- ARE GAUSSIANS
C
      IF(KPK.LT.1.OR.I1.GT.I2) RETURN
C
      DO 10 J=1,KPK
      WDSQ(J)=HWID(J)*HWID(J)
      IF(WDSQ(J).LT.0.01) WDSQ(J)=0.01
   10 CONTINUE
C
C     GENERATE GAUSSIANS WITH PEAKS AT XP
C
      DO 30 J=1,KPK
      JN=J+1
      DO 30 I=I1,I2
      XG=I
      G=(XG-XP(J))**2/WDSQ(J)
      G=25.0*G+1.5
      IF(G.GT.200.0) G=200.0
      NDX=G
   30 SPECT(I,JN)=GAU(NDX)
      NMAX=JN+2
      IF(KNBAK.EQ.0) NMAX=JN
C
C     CALCULATE MATRIX ELEMENTS FOR DMAINV8(M,N)
C
      DO 40 N=2,NMAX
      J=N-1
      SUM=0.0
      DO 38 I=I1,I2
   38 SUM=SUM+WT(I)*SPECT(I,1)*SPECT(I,N)
      B(J,1)=SUM
   40 CONTINUE
      NUP=NMAX-1
      DO 60 I=1,NUP
      DO 58 J=1,I
      SUM=0
      DO 56 K=I1,I2
      SUM=SUM+WT(K)*SPECT(K,I+1)*SPECT(K,J+1)
   56 CONTINUE
      A(I,J)=SUM
      A(J,I)=SUM
   58 CONTINUE
   60 CONTINUE
      CALL DMAINV8(NUP,1)
      DO 70 I=1,NUP
   70 BETA(I)=B(I,1)
C
C     CALCULATE BACKGROUND AND YCAL
C
      DO 72 I=I1,I2
      YCAL(I)=0.0
   72 BGD(I)=0.0
      IF(KNBAK.EQ.0) GO TO 95
      J1=KPK+2
      J2=J1+1
      DO 90 J=J1,J2
      DO 90 I=I1,I2
   90 BGD(I)=BGD(I)+SPECT(I,J)*BETA(J-1)
   95 J1=2
      J2=NUP+1
      DO 100 J=J1,J2
      DO 100 I=I1,I2
  100 YCAL(I)=YCAL(I)+SPECT(I,J)*BETA(J-1)
C
C     CALCULATE QFN
C
      SUM=0
      DO 120 I=I1,I2
  120 SUM=SUM+(YCAL(I)-SPECT(I,1))**2*WT(I)
      DENOM=I2-I1-KPK-2+2*IFBGD
      IF(DENOM.LT.1.0) DENOM=1.0
      QFN=SUM/DENOM
      RETURN
      END
