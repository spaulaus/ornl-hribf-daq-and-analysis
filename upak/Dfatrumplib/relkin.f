C$PROG RELKIN
      SUBROUTINE RELKIN  (XM1A,XM2A,XM3A,XM4A,EIN,THETA,THCM31,CMTOL1 ,E
     131,Q,IJ)
      IMPLICIT REAL*8(A-H,O-Z)
      COMMON/AAA/ Z(4), NZ(30), CONST(50), ZZ(25)
      DIMENSION CMTOLB(2), E3(2), THCM3(2), E4(2), THCM4(2), THETA4(2)
      EQUIVALENCE (CONST(19),DEGRAD),(CONST(34),XA)
C   
      EQUIVALENCE (SOLN2 ,ZZ(1)),
     &            (TWO   ,ZZ(13))
C   
      DATA TRUE,FALSE/1.0,0.0/
C     INPUT MASSES IN C12 AMU UNITS
C     INPUT LAB ANGLES IN DEGREES
C     INPUT Q VALUES AND ENERGIES IN MEV
C   
      IJ=0
      IF (XM1A) 1,1,2
1     RETURN
C     DEFINE PI
2     PI=3.141592654
C   
      EN=EIN
      T=THETA
C     CHANGE REST MASSES TO ENERGY
      XM1=XM1A*XA
      XM2=XM2A*XA
      XM3=XM3A*XA
      XM4B=XM4A*XA
C     QGS = GROUND STATE Q VALUE
      QGS=XM1+XM2-XM3-XM4B
C   
C      THIS IS A CHECK TO INSURE REASONABLE Q VALUES AND MASSES
C     INPUT Q SHOULD BE LESS THAN OR EQUAL TO QGS
      IF (.01-(Q-QGS)) 3,3,4
 3    WRITE(7,20)
      RETURN
C     GO TO 13
C   
C     IF Q IS LESS THAN QGS, IT IS ASSUMED THAT RESIDUAL NUCLEUS( PARTIC
C     LE 4 ) IS IN EXCITED STATE OF QGS-Q . ADD THIS ENERGY TO MASS.
4     XM4=XM4B+QGS-Q
C   
C     DEFINE QUANTITIES TO BE USED IN LATER EQUATIONS
      E1=EIN+XM1
C     CALCULATE THRESHOLD ENERGY
      ETH=-Q*(XM1+XM2+XM3+XM4)/(2.*XM2)+XM1
C   
C     THE NEXT STATEMENT IS A CHECK FOR THRESHOLD VALUES
      IF (E1-ETH) 5,5,6
 5    WRITE(7,21)EIN
      GO TO 19
C   
6     ET=E1+XM2
      P1=DSQRT(E1**2-XM1**2)
      THETA=T
C   
C     THE REST OF THE PROGRAM DOWN TO STATEMENT 5 COMPUTES THE ANGLES
C     AND ENERGIES
C   
C   
      SITH3=DSIN(THETA)
      COTH3=DCOS(THETA)
      TA=THETA/DEGRAD
C     CHECK TO SEE IF PARTICLE HAS ENOUGH ENERGY FOR ANGLE
      CHECK=(XM2*E1+(XM1**2+XM2**2-XM3**2-XM4**2)/2.)**2-(XM3*XM4)**2-(P
     11*XM3*SITH3)**2
      IF (CHECK) 7,8,8
 7    WRITE(7,22)EIN,TA
      GO TO 19
C   
C     CALCULATE CENTER OF MASS ENERGIES
8     ECMT=DSQRT(XM1**2+XM2**2+2.*XM2*E1)
      ECM1=(XM1**2+XM2*E1)/ECMT
      ECM2=(XM2**2+XM2*E1)/ECMT
      ECM3=(ECMT**2+XM3**2-XM4**2)/(2.*ECMT)
      ECM4=(ECMT**2+XM4**2-XM3**2)/(2.*ECMT)
C   
C     PCM = CENTER OF MASS MOMENTUM OF OUTGOING PARTICLES
      PCM=DSQRT(ECM4**2-XM4**2)
C   
C     ALPHA DETERMINES IF THERE IS ONE OR TWO SOLUTIONS FOR E3 AT ANGLE
C     THETA3
      ALPHA=P1*(1.+(XM3**2-XM4**2)/ECMT**2)/((ET)*DSQRT((1.-((XM3+XM4)/E
     1CMT)**2)*(1.-((XM3-XM4)/ECMT)**2)))
C   
      IF (ALPHA-1.000001) 9,9,10
9     IJ=1
C     ONE SOLUTION FOR E3
      GO TO 11
C   
10    IJ=2
C     TWO SOLUTIONS FOR E3
11    DO 16 I=1,IJ
C   
C     CALCULATION OF ENERGY OF OUTGOING PARTICLE 3
      FI=I
      E3(I)=(ET*(XM2*E1+(XM1**2+XM2**2+XM3**2-XM4**2)/2.)+(3.-2.*FI)*P1*
     1COTH3*DSQRT(CHECK))/(ET**2-(P1*COTH3)**2)
C   
C     SINCE ET = E3 + E4
      E4(I)=ET-E3(I)
C   
C      CALCULATE MOMENTA OF PARTICLES 3 AND 4
      P3=DSQRT(E3(I)**2-XM3**2)
      P4=DSQRT(E4(I)**2-XM4**2)
C   
      BETA=P1/ET
      GAMMA=ET/ECMT
C   
C     CALCULATE CENTER OF MASS SCATTERING ANGLES OF PARTICLES 3 AND 4
      SICM=P3*SITH3/PCM
      COCM=GAMMA*(P3*COTH3-BETA*E3(I))/PCM
      THCM3(I)=ATAN(SICM/COCM)
      THCM4(I)=PI-THCM3(I)
C   
C     CALCULATE LAB SCATTERING ANGLE OF PARTICLE 4
C     IF E4 = 0 THEN THETA(4) = PI/2
      THETA4(I)=PI/2.
      IF (E4(I)-XM4-1.E-6) 13,13,12
12    SITH4=P3*SITH3/P4
      COTH4=(P1-P3*COTH3)/P4
      THETA4(I)=ATAN(SITH4/COTH4)
C   
C     DETERMINE CM TO LAB RATIO OF DIFFERENTIAL CROSS SECTION
13    FI=I
      CMTOLB(I)=(3.-2.*FI)*ET/ECMT*(1.+P1/ET*(ET**2-P1**2+XM3**2-XM4**2)
     1/DSQRT((ET**2-P1**2+XM3**2-XM4**2)**2-4.*ECMT**2*XM3**2)*COCM)*PCM
     2**3/P3**3
C   
C     CHANGE ANGLES TO DEGREES
      THCM4(I)=THCM4(I)*180.0/PI
      THCM3(I)=THCM3(I)*180.0/PI
      THETA4(I)=THETA4(I)*180.0/PI
      IF (THCM3(I)) 14,15,15
14    THCM3(I)=THCM3(I)+180.
15    CONTINUE
C   
C     CHANGE ENERGIES TO KINETIC (MEV)
      E3(I)=E3(I)-XM3
      E4(I)=E4(I)-XM4
16    CONTINUE
      ECM1=ECM1-XM1
      ECM2=ECM2-XM2
      ECM3=ECM3-XM3
      ECM4=ECM4-XM4
      IF (IJ.EQ.1) GO TO 17
      IF(SOLN2.EQ.1.0) GO TO 18
17    CONTINUE
      THCM31=THCM3(1)
      CMTOL1 =CMTOLB(1)
      E31=E3(1)
      IF(IJ.EQ.2) TWO = TRUE
      GO TO 19
18    THCM31=THCM3(2)
      CMTOL1 =CMTOLB(2)
      E31=E3(2)
19    CONTINUE
      RETURN
C   
C   
20    FORMAT (/////41H EITHER MASS VALUES OR Q VALUE WAS BOOTED)
21    FORMAT (/25H LAB BOMBARDING ENERGY = ,F10.5,39H MEV IS BELOW REACT
     1ION THRESHOLD ENERGY,I5)
22    FORMAT (25H LAB BOMBARDING ENERGY = ,F10.5,69H MEV IS ABOVE REACTI
     1ON THRESHOLD ENERGY,BUT TOO LOW FOR THETA LAB =  ,F10.5,I5)
      END
